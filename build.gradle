plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

group = 'io.arconia'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://central.sonatype.com/repository/maven-snapshots/")
    }
}

dependencies {
    // Arconia Framework
    implementation 'io.arconia:arconia-spring-boot-starter'
    implementation 'io.arconia:arconia-opentelemetry-spring-boot-starter'

    // Spring Boot Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // PostgreSQL with Spring Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    runtimeOnly 'org.postgresql:postgresql'

    // Redis with Spring Data Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Kafka with Spring Kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // Development tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Arconia Dev Services for local development
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-postgresql'
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-redis'
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-kafka'
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-lgtm'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "io.arconia:arconia-bom:0.19.0"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootBuildImage') {
    builder = "paketobuildpacks/builder-noble-java-tiny"
}

springBoot {
    buildInfo {
        excludes = ['time']
    }
}
